plugins {
    id 'fabric-loom' version '1.15-SNAPSHOT' apply false
    id 'maven-publish'
}

subprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'

    version = rootProject.mod_version
    group = rootProject.maven_group

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    }

    processResources {
        def replaceProperties = [
                version: rootProject.mod_version,
                minecraft_version: rootProject.minecraft_version,
                loader_version: rootProject.loader_version,
        ]
        inputs.properties replaceProperties
        filesMatching("fabric.mod.json") { expand replaceProperties }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    tasks.withType(Jar).configureEach {
        from(rootProject.file("LICENSE.txt")) {
            rename { "LICENSE" }
        }
    }
}